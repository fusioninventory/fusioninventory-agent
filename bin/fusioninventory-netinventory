#!/usr/bin/perl

use strict;
use warnings;
use lib './lib';

use English qw(-no_match_vars);
use Getopt::Long;
use Pod::Usage;
use XML::TreePP;

use FusionInventory::Agent;
use FusionInventory::Agent::Logger;
use FusionInventory::Agent::Recipient;
use FusionInventory::Agent::Task::NetInventory;
use FusionInventory::Agent::Tools;
use FusionInventory::Agent::Tools::Hardware;

my %setup = (
    confdir => './etc',
    datadir => './share',
    libdir  => './lib',
    vardir  => './var',
);

my $options = {
    threads => 1
};

GetOptions(
    $options,
    'model=s',
    'community=s',
    'credential=s',
    'entity=s',
    'threads=i',
    'recipient=s',
    'timeout=i',
    'verbose',
    'debug+',
    'help',
    'version',
) or pod2usage(-verbose => 0);

if ($options->{version}) {
  print "NetInventory task $FusionInventory::Agent::Task::NetInventory::VERSION\n";
  exit 0;
}
pod2usage(-verbose => 0, -exitval => 0) if $options->{help};

pod2usage(
    -message => "no target given, aborting\n", -verbose => 0
) unless @ARGV;

my $agent = FusionInventory::Agent->new(%setup);
$agent->init(options => $options);

my $device_id = 1;
my @devices;
foreach my $arg (@ARGV) {
    my $device = {
        authsnmp_id  => 1,
        modelsnmp_id => 1
    };
    foreach my $parameter (split(',', $arg)) {
        my ($key, $value) = split(':', $parameter);
        if (!$value) {
            $device->{ip} = $key;
        } else {
            $device->{$key} = $value;
        }
    }
    $device->{id}     = $device_id++ unless $device->{id};
    $device->{entity} = $options->{entity} if $options->{entity};
    push @devices, $device;
}

my @models;

if ($options->{model}) {
    pod2usage(
        -message => "invalid file '$options->{model}', aborting\n",
        -verbose => 0
    ) unless -f $options->{model};
    push @models, loadModel($options->{model});
}

my $credentials_id = 1;
my @credentials;
if ($options->{community}) {
    foreach my $community (@{$options->{community}}) {
        push @credentials,
            { id => $credentials_id++, version => 1, community => $community };
    }
} elsif ($options->{credential}) {
    foreach my $specification (@{$options->{credential}}) {
        my $credential = { id => $credentials_id++ };
        foreach my $parameter (split(',', $specification)) {
            my ($key, $value) = split(':', $parameter);
            $credential->{$key} = $value;
        }
        push @credentials, $credential;
    }
} else {
    push @credentials, {
        id => $credentials_id++, version => 1, community => 'public'
    };
}

my $task = FusionInventory::Agent::Task::NetInventory->new(
    logger => $agent->{logger},
);

$task->configure(
    datadir     => $setup{datadir},
    deviceid    => $agent->{deviceid},
    pid         => 1,
    threads     => $options->{threads},
    timeout     => $options->{timeout},
    devices     => \@devices,
    models      => \@models,
    credentials => \@credentials
);

my $recipient = FusionInventory::Agent::Recipient->create(
    target  => $options->{recipient},
    verbose => $options->{verbose}
);

eval {
    $task->run(recipient => $recipient);
};
if ($EVAL_ERROR) {
    print STDERR "Execution failure:.\n";
    print STDERR $EVAL_ERROR;
    exit 1;
}

__END__

=head1 NAME

fusioninventory-netinventory - Standalone network inventory

=head1 SYNOPSIS

B<fusioninventory-netinventory> S<[B<--model> I<file>]>
S<[B<--credential> I<string>]> S<[B<--timeout> I<integer>]>
S<[B<--threads> I<integer>]> S<[B<--entity> I<string>]>
S<[B<--recipient> I<string>]> S<[B<--verbose>]> S<[B<--debug>]>
I<host_spec> ...

=head1 OPTIONS

=over

=item B<--model> I<file>

model file

=item B<--credential> I<string>

SNMP credentials (default: version:1,community:public)

=item B<--timeout> I<integer>

SNMP timeout (default: 15s)

=item B<--threads> I<integer>

threads number (default: 1)

=item B<--entity> I<string>

GLPI entity

=item B<--recipient> I<string>

where to send result (default: stdout)

=item B<--verbose>

verbose output (control messages)

=item B<--debug>

debug output (execution traces)

=back

=head1 EXAMPLES

    $> fusioninventory-netinventory 192.168.1.24
    $> fusioninventory-netinventory 192.168.1.24 
        --recipient /tmp
    $> fusioninventory-netinventory 192.168.1.24
        --recipient http://glpi.domain.com/plugins/fusioninventory
    $> fusioninventory-netinventory host:192.168.1.24,id:1,type:network
    $> fusioninventory-netinventory host:192.168.1.24,id:1,type:network
        --credential version:3,username:foo,authprotocol:sha1,authpassword:bar
    $> fusioninventory-netinventory 192.168.1.24 192.168.1.25
    $> fusioninventory-netinventory host:192.168.1.24,id:1 host:192.168.1.25,id:2

=head1 DESCRIPTION

F<fusioninventory-netinventory> allows to run a network inventory task without
a GLPI server.
