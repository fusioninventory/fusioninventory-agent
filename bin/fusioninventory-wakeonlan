#!/usr/bin/perl

use strict;
use warnings;
use lib './lib';

use English qw(-no_match_vars);
use Getopt::Long;
use Pod::Usage;

use FusionInventory::Agent;
use FusionInventory::Agent::Task::WakeOnLan;

my %setup = (
    confdir => './etc',
    datadir => './share',
    libdir  => './lib',
    vardir  => './var',
);

my $options = {
    ethernet => 1,
    udp      => 1,
};

GetOptions(
    $options,
    'ethernet!',
    'udp!',
    'debug+',
    'help',
    'version',
) or pod2usage(-verbose => 0);

if ($options->{version}) {
  print "WakeOnLan task $FusionInventory::Agent::Task::WakeOnLan::VERSION\n";
  exit 0;
}
pod2usage(-verbose => 0, -exitval => 0) if $options->{help};
pod2usage(
    -message => "no mac address given, aborting\n", -verbose => 0
) unless @ARGV;

my $agent = FusionInventory::Agent->new(%setup);
$agent->init(options => $options);

my $task = FusionInventory::Agent::Task::WakeOnLan->new(
    logger   => $agent->{logger},
);

$task->configure(
    datadir   => $agent->{datadir},
    deviceid  => $agent->{deviceid},
    addresses => [ @ARGV ],
    ethernet  => $options->{ethernet},
    udp       => $options->{udp}
);

eval {
    $task->run(
        user         => $agent->{config}->{user},
        password     => $agent->{config}->{password},
        proxy        => $agent->{config}->{proxy},
        ca_cert_file => $agent->{config}->{'ca-cert-file'},
        ca_cert_dir  => $agent->{config}->{'ca-cert-dir'},
        no_ssl_check => $agent->{config}->{'no-ssl-check'},
    );
};

__END__

=head1 NAME

fusioninventory-wakeonlan - Standalone wake-on-lan

=head1 SYNOPSIS

fusioninventory-wakeonlan [options] <address> <address>

  Options:
    --ethernet        use ethenet method if available (true)
    --udp             use udp method (true)
    --debug           debug output (execution traces)
    -h --help         print this message and exit
    --version         print the task version and exit

=head1 DESCRIPTION

F<fusioninventory-wakeonlan> allows to run a wakeonlan task without a GLPI
server.
