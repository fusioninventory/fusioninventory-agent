#!/usr/bin/perl

use strict;
use warnings;
use lib './lib';

use English qw(-no_match_vars);
use Getopt::Long;
use File::Temp;
use Pod::Usage;

use FusionInventory::Agent::Broker::Inventory;
use FusionInventory::Agent::Config;
use FusionInventory::Agent::Logger;
use FusionInventory::Agent::Task::Inventory;

my %setup = (
    confdir => './etc',
    datadir => './share',
    libdir  => './lib',
    vardir  => './var',
);

my $options = {
    config => 'none'
};

GetOptions(
    $options,
    'additional-content=s',
    'collect-timeout=s',
    'html',
    'no-category=s',
    'scan-homedirs',
    'tag|t=s',
    'target=s',
    'verbose',
    'debug+',
    'help',
    'version',
) or pod2usage(-verbose => 0);

if ($options->{version}) {
  print "Inventory task $FusionInventory::Agent::Task::Inventory::VERSION\n";
  exit 0;
}
pod2usage(-verbose => 0, -exitval => 0) if $options->{help};

my $config = FusionInventory::Agent::Config->new(
    options => $options,
);

my $inventory = FusionInventory::Agent::Task::Inventory->new(
    target  => {},
    logger  => FusionInventory::Agent::Logger->new(debug => $options->{debug}),
    datadir => $setup{datadir},
    confdir => $setup{confdir},
    config  => $config
);

my $broker = FusionInventory::Agent::Broker::Inventory->create(
    target  => $options->{target},
    task    => 'inventory',
    verbose => $options->{verbose}
);

$inventory->run(broker => $broker);

__END__

=head1 NAME

fusioninventory-inventory - Standalone inventory

=head1 SYNOPSIS

fusioninventory-inventory [options]

  Options:
    --scan-homedirs                scan use home directories (false)
    --html                         save the inventory as HTML (false)
    -t --tag tag                   inventory tag
    --collect-timeout time         inventory modules execution timeout (30)
    --additional-content file      additional inventory content file
    --verbose                      verbose output (control messages)
    --debug                        debug output (execution traces)
    -h --help                      print this message and exit
    --version                      print the task version and exit

=head1 DESCRIPTION

F<fusioninventory-inventory> allows to run an inventory task without a GLPI
server.
