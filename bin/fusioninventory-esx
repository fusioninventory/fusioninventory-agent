#!/usr/bin/perl

use strict;
use warnings;
use lib './lib';

use English qw(-no_match_vars) ;
use Getopt::Long;
use Pod::Usage;

use FusionInventory::Agent;
use FusionInventory::Agent::Recipient;
use FusionInventory::Agent::Task::ESX;

my %setup = (
    confdir => './etc',
    datadir => './share',
    libdir  => './lib',
    vardir  => './var',
);

my $options = {
};

GetOptions(
    $options,
    'recipient=s',
    'directory=s', # deprecated
    'tag|t=s',
    'help',
    'version',
) or pod2usage(-verbose => 0);

if ($options->{version}) {
    print "ESX inventory task $FusionInventory::Agent::Task::ESX::VERSION\n";
    exit 0;
}

pod2usage(-verbose => 0, -exitstatus => 0) if $options->{help};
pod2usage(
    -message => "no host given, aborting\n", -verbose => 0
) unless @ARGV;

if ($options->{directory}) {
    $options->{recipient}=$options->{directory};
    print STDERR "--directory is deprecated, please use --recipient instead";
}

my $agent = FusionInventory::Agent->new(%setup);
$agent->init(options => $options);

my @jobs;
foreach my $arg (@ARGV) {
    my $job;
     foreach my $parameter (split(',', $arg)) {
        my ($key, $value) = split(':', $parameter);
        $job->{$key} = $value;
    }
    push @jobs, $job;
}

my $task = FusionInventory::Agent::Task::ESX->new(
    logger => $agent->{logger},
);

$task->configure(
    datadir  => $agent->{datadir},
    deviceid => $agent->{deviceid},
    jobs     => \@jobs
);

my $recipient = FusionInventory::Agent::Recipient->create(
    target  => $options->{recipient},
    verbose => $options->{verbose}
);

eval {
    $task->run(recipient => $recipient);
};
if ($EVAL_ERROR) {
    print STDERR "Execution failure:.\n";
    print STDERR $EVAL_ERROR;
    exit 1;
}

__END__

=head1 NAME

fusioninventory-esx - standaline ESX inventory

=head1 SYNOPSIS

fusioninventory-esx [options] <host> <host>

  Options:
    --recipient string     where to send result (default: stdout)
    -t --tag tag           inventory tag
    -h --help              print this message and exit
    --version              print the task version and exit

=head1 EXAMPLES

    $> fusioninventory-esx host:myesx,user:foo,password:bar
    $> fusioninventory-esx host:myesx,user:foo,password:bar
        --recipient /tmp
    $> fusioninventory-esx host:myesx,user:foo,password:bar
        --recipient http://glpi.domain.com/plugins/fusioninventory
    $> fusioninventory-esx host:esx1,user:foo,password:bar host:esx2,user:foo,password:bar

=head1 DESCRIPTION

F<fusioninventory-esx> allows to run an ESX inventory task without a GLPI
server.
