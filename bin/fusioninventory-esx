#!/usr/bin/perl

use strict;
use warnings;
use lib './lib';

use English qw(-no_match_vars) ;
use Getopt::Long;
use Pod::Usage;

use FusionInventory::Agent;
use FusionInventory::Agent::Recipient;
use FusionInventory::Agent::Task::ESX;

my %setup = (
    confdir => './etc',
    datadir => './share',
    libdir  => './lib',
    vardir  => './var',
);

my $options = {
};

GetOptions(
    $options,
    'host=s',
    'user=s',
    'password=s',
    'recipient=s',
    'directory=s', # deprecated
    'tag|t=s',
    'help',
    'version',
) or pod2usage(-verbose => 0);

pod2usage(-verbose => 0, -exitstatus => 0) if $options->{help};

if ($options->{version}) {
    print "ESX inventory task $FusionInventory::Agent::Task::ESX::VERSION\n";
    exit 0;
}

pod2usage(-verbose => 0) unless
    $options->{host}      and
    $options->{user}      and
    $options->{password};

if ($options->{directory}) {
    $options->{recipient}=$options->{directory};
    print STDERR "--directory is deprecated, please use --recipient instead";
}

my $agent = FusionInventory::Agent->new(%setup);
$agent->init(options => $options);

my $task = FusionInventory::Agent::Task::ESX->new(
    logger => $agent->{logger},
);

$task->configure(
    datadir  => $agent->{datadir},
    deviceid => $agent->{deviceid},
    jobs     => [
        {
            host     => $options->{host},
            user     => $options->{user},
            password => $options->{password}
        }
    ]
);

my $recipient = FusionInventory::Agent::Recipient->create(
    target  => $options->{recipient},
    task    => 'netdiscovery',
    verbose => $options->{verbose}
);

eval {
    $task->run(
        user         => $agent->{config}->{user},
        password     => $agent->{config}->{password},
        proxy        => $agent->{config}->{proxy},
        ca_cert_file => $agent->{config}->{'ca-cert-file'},
        ca_cert_dir  => $agent->{config}->{'ca-cert-dir'},
        no_ssl_check => $agent->{config}->{'no-ssl-check'},
        recipient    => $recipient
    );
};

__END__

=head1 NAME

fusioninventory-esx - vCenter/ESX/ESXi remote inventory from command line

=head1 SYNOPSIS

fusioninventory-esx --host <host> --user <user> --password <password>

  Options:
    --host host            ESX server host name
    --user user            ESX server user name
    --password xxxx        ESX server user password
    --recipient string     where to send result (default: stdout)
    -t --tag tag           inventory tag
    -h --help              print this message and exit
    --version              print the task version and exit

=head1 EXAMPLES

    % fusioninventory-esx --host myesx --user foo --password bar --directory /tmp


You can import the .ocs file in your inventory server with the fusioninventory-injector tool.
    %fusioninventory-injector -v --file /tmp/*.ocs -u https://myserver/ocsinventory

=head1 DESCRIPTION

F<fusioninventory-esx> creates inventory of remote ESX/ESXi and vCenter VMware.
It uses the SOAP interface of the remote server.

Supported systems:

=over 4

=item F<ESX and ESXi 3.5>

=item F<ESX and ESXi 4.1>

=item F<ESXi 5.0>

=item F<vCenter 4.1>

=item F<vCenter 5.0>

=back


Active Directory users, please note the AD authentication doesn't work. You must
create a account on the VMware server.

=head1 LIMITATION

So far, ESX serial number are not collected.

=head1 SECURITY

The SSL hostname check of the server is disabled.
